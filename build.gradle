plugins {
    id "com.jfrog.artifactory" version '4.15.1'
    id 'org.jetbrains.kotlin.jvm' version "1.3.72"
    id 'java-gradle-plugin'
    id 'antlr'
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

ext {
    sharedDir           = file("${project.rootDir}/shared")
}

repositories {
    maven {
        name 'MavenLocal'
        url sharedDir
    }
    mavenCentral()
    maven {
        name 'Artifactory snapshot'
        url 'http://artifactory5.exp.exactpro.com/artifactory/libs-snapshot'
    }
    maven {
        name 'Artifactory release'
        url 'http://artifactory5.exp.exactpro.com/artifactory/libs-release'
    }
    mavenLocal()

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

group = 'com.exactpro.th2'
version = release_version

sourceCompatibility = 11
targetCompatibility = 11

sourceSets {
    main.kotlin.srcDirs += "src/main/kotlin"
    main.java.srcDirs += "build/generated-src/antlr/main"
}


dependencies {
    api platform('com.exactpro.th2:bom:2.0.+')
    implementation "io.grpc:grpc-stub"

    implementation 'org.slf4j:slf4j-api'

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    antlr group: 'org.antlr', name: 'antlr4', version: '4.5'

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.1"
}

manifest {
    attributes(
            'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
            'Specification-Title':      '',
            'Specification-Vendor':     'Exactpro Systems LLC',
            'Implementation-Title':     project.archivesBaseName,
            'Implementation-Vendor':    'Exactpro Systems LLC',
            'Implementation-Vendor-Id': 'com.exactpro',
            'Implementation-Version':   project.version
    )
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    (options as StandardJavadocDocletOptions).addBooleanOption("html5", true)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = 'localRepo'
            url = sharedDir
        }
    }
}

artifactory {
    publish {
        contextUrl = "${artifactory_url}"
        repository {
            repoKey = "${artifactory_deploy_repo_key}" // The Artifactory repository key to publish to
            username = "${artifactory_user}" // The publisher user name
            password = "${artifactory_password}" // The publisher password
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('mavenJava')
            publishArtifacts = true
            publishBuildInfo = true
            // Properties to be attached to the published artifacts.
            publishPom = true
        }
    }
}

generateGrammarSource {
    arguments += ["-package", "com.exactpro.th2.proto.service.generator.core.antlr"]
}

compileJava.dependsOn.add('generateGrammarSource')

gradlePlugin {
    plugins {
        serviceGeneratorPlugin {
            id = 'com.exactpro.th2.service-generator'
            implementationClass = 'com.exactpro.th2.proto.service.generator.plugin.ServiceGeneratorPlugin'
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
