plugins {
    id "com.jfrog.bintray" version '1.8.5'
    id 'org.jetbrains.kotlin.jvm' version "1.3.72"
    id 'java-gradle-plugin'
    id 'antlr'
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

ext {
    sharedDir           = file("${project.rootDir}/shared")
}

repositories {
    maven {
        name 'MavenLocal'
        url sharedDir
    }
    mavenCentral()
    maven {
        name 'Bintray'
        url 'https://dl.bintray.com/exactpro/th2'
    }
    mavenLocal()

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

group = 'com.exactpro.th2'
version = release_version

sourceCompatibility = 11
targetCompatibility = 11

sourceSets {
    main {
//        kotlin {
//            srcDirs=["src/main/kotlin"]
//        }

        java {
            srcDirs=["src/main/java", "build/generated-src/antlr/main"]
        }
    }
}


dependencies {
    api platform('com.exactpro.th2:bom:2.2.+')
    implementation "io.grpc:grpc-stub"

    implementation 'org.slf4j:slf4j-api'

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    antlr group: 'org.antlr', name: 'antlr4', version: '4.5'

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.1"
}

manifest {
    attributes(
            'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
            'Specification-Title':      '',
            'Specification-Vendor':     'Exactpro Systems LLC',
            'Implementation-Title':     project.archivesBaseName,
            'Implementation-Vendor':    'Exactpro Systems LLC',
            'Implementation-Vendor-Id': 'com.exactpro',
            'Implementation-Version':   project.version
    )
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    (options as StandardJavadocDocletOptions).addBooleanOption("html5", true)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
        bintrayPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId rootProject.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('name', rootProject.name)
                root.appendNode('url', "${vcs_url}")
            }
        }
    }

    repositories {
        maven {
            name = 'localRepo'
            url = sharedDir
        }
    }
}

bintray {
    user = "${bintray_user}"
    key = "${bintray_key}"
    publications = ['bintrayPublication']
    pkg {
        repo = 'th2'
        name = rootProject.name
        userOrg = 'exactpro'
        licenses = ['Apache-2.0']
        vcsUrl = "${vcs_url}"
        version {
            name = project.version
            desc = project.version
            released  = new Date()
        }
    }
    publish = true
}

generateGrammarSource {
    arguments += ["-package", "com.exactpro.th2.proto.service.generator.core.antlr"]
}

compileJava.dependsOn.add('generateGrammarSource')

gradlePlugin {
    plugins {
        serviceGeneratorPlugin {
            id = 'com.exactpro.th2.service-generator'
            implementationClass = 'com.exactpro.th2.proto.service.generator.plugin.ServiceGeneratorPlugin'
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
